-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun getVar (state, name) {
  case state.lookup (name) of
    Var (v) -> v
  | _       -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun getFun (state, name) {
  case state.lookup (name) of
    f@Fun (_, _) -> f
  | _          -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Var (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}


fun addArgs(c@[state, world], args, vals) {
  if size(args) != size(vals) then
    failure("Args and Vals must be equal size")
  fi;

  case args of
      {}    -> c
    | arg : args  ->
      case vals of
        value : vals  -> addArgs([state.addName(arg, value), world], args, vals)
      esac
  esac
}

fun addDefs (state, defs) {
  foldl (fun (state, def) {
    case def of
        Var (names)            -> addNames (state, names)
      | Fun (name, args, body) -> addFunction (state, name, args, body)
      | _                      -> failure ("Unexcepted definition: %s\n", def.string)
    esac
  }, state, defs)
}

(* Assignment *)
fun eval(c@[state, world], expr) {
  case expr of
      Assn  (expr1, expr2)            ->
        case evalList(c, { expr1, expr2 }) of
          [[state, world], { RefVar(x), v }] -> [[state <-[x, v], world], v]
        esac
    | Seq   (expr1, expr2)            ->
        case eval(c, expr1) of
          [c, _] -> eval(c, expr2)
        esac
    | Skip                            -> [c, Void]
    | Read (x)                        ->
        case readWorld(world) of
          [v, newWorld] -> [[state <-[x, v], newWorld], Void]
        esac
    | Write (expr)                    ->
        case eval(c, expr) of
          [[state, world], v] -> [[state, writeWorld(v, world)], Void]
        esac
    | If (ifExpr, thenExpr, elseExpr) ->
        case eval(c, ifExpr) of
            [c, 0] -> eval(c, elseExpr)
          | [c, _] -> eval(c, thenExpr)
        esac
    | While (expr1, expr2)            ->
        case eval(c, expr1) of
            [c, 0] -> [c, Void]
          | [c, _] -> eval(c, Seq (expr2, expr))
        esac
    | DoWhile (expr1, expr2)          -> eval(c, Seq(expr1, While(expr2, expr1)))
    | Var (x)                         -> [c, state.lookup(x)]
    | Ref (x)                         -> [c, RefVar(x)]
    | Const (v)                       -> [c, v]
    | Binop (o, e1, e2) ->
        case eval(c, e1) of
          [c, v1] ->
            case eval(c, e2) of
              [c, v2] -> [c, evalOp(o, v1, v2)]
            esac
        esac
    | Ignore (expr)                   ->
        case eval(c, expr) of
          [c, _] -> [c, Void]
        esac
    | Call (name, exprs) ->
        case evalList(c, exprs) of
          [[state, world], vals] ->
            case state.lookup(name) of
              Fun(args, body) ->
                case state.enterFunction of
                  sGlobal ->
                    case addArgs([sGlobal, world], args, vals) of
                      cLocal ->
                        case eval(cLocal, body) of
                          [[sLocal, wLocal], res] -> [[state.leaveFunction(sLocal.getGlobal), wLocal], res]
                        esac
                    esac
                esac
            esac
        esac
    | Scope (defs, expr) ->
        case state.enterScope of
          state ->
            case state.addDefs(defs) of
              state ->
                case eval([state, world], expr) of
                  [[state, world], v] -> [[state.leaveScope, world], v]
                esac
            esac
        esac
    | _                               -> failure("Unexcepted expression %s\n", expr.string)
  esac
}
(* End *)

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState (), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}